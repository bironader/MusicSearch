import static ApplicationProperties.*
import static Dependencies.*
import static Versions.JACOCO

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-parcelize'
    id 'androidx.navigation.safeargs.kotlin'
    id "org.sonarqube" version "2.8"
    id 'jacoco'

}


jacoco {
    toolVersion = JACOCO
}
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
    }

    def fileFilter = [
            // data binding
            'android/databinding/**/*.class',
            '**/android/databinding/*Binding.class',
            '**/android/databinding/*',
            '**/androidx/databinding/*',
            '**/BR.*',
            // android
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/Manifest*.*',
            '**/*Test*.*',
            'android/**/*.*',
            // dagger
            '**/*_MembersInjector.class',
            '**/Dagger*Component.class',
            '**/Dagger*Component$Builder.class',
            '**/*Module_*Factory.class',
            '**/*_Factory*.*',
            '**/*Module*.*',
            '**/*Dagger*.*',
            '**/*Hilt*.*',
            '**/di/*Module*.*',
            '**/*Module*.*',
            // kotlin
            '**/*MapperImpl*.*',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/BuildConfig.*',
            '**/*Component*.*',
            '**/*BR*.*',
            '**/Manifest*.*',
            '**/*$Lambda$*.*',
            '**/*Companion*.*',
            '**/*Module*.*',
            '**/*Dagger*.*',
            '**/*Hilt*.*',
            '**/*MembersInjector*.*',
            '**/*_MembersInjector.class',
            '**/*_Factory*.*',
            '**/*_Provide*Factory*.*',
            '**/*Extensions*.*',
            // sealed and data classes
            '**/*$Result.*',
            '**/*$Result$*.*'
    ]
    def debugTree = fileTree(dir: "$project.buildDir/generated", excludes: fileFilter)
    def mainSrc = fileTree(dir: "$project.projectDir/src/main/java", excludes: fileFilter)

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec', 'outputs/code_coverage/debugAndroidTest/connected/**/*.ec'
    ]))
    jacoco {
        toolVersion = JACOCO
    }

    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
        jacoco.excludes = ['jdk.internal.*']
    }


}
android {
    compileSdkVersion COMPLIED_SDK_VERSION
    buildToolsVersion BUILD_TOOL_VERSION

    defaultConfig {
        applicationId
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode VERSION_CODE
        versionName VERSION_NAME

        buildConfigField("String", "GATWAY_KEY", GATEWAY_KEY)
        buildConfigField("String", "BASE_URL", "\"http://staging-gateway.mondiamedia.com/\"")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug{
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        dataBinding = true
    }

    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {


    implementation KTS
    implementation KOTLIN
    implementation APP_COMPAT
    implementation CONSTRAINT_LAYOUT
    implementation HILT
    implementation GSON
    implementation MATERIAL
    implementation NAV_FRAGMENT
    implementation NAV_UI
    implementation GLIDE
    implementation LIFE_CYLCE_VIEWMODEL
    implementation LIFE_CYLCE_EXT
    implementation LIVEDATA_EXT
    implementation HILT_LIFE_CYCLE
    implementation COROUTINES
    implementation COROUTINES_CORE

    implementation SWIPE_REFRESH
    implementation ESPRESSO_IDLING_RESOURCE


    kapt AnnotationProcessing.HILT_COMPILER
    kapt AnnotationProcessing.HILT_LIFECYCLE_COMPLIER
    kapt AnnotationProcessing.GLIDE_COMPILER

    testImplementation TestDependencies.JUNIT
    testImplementation TestDependencies.MOCKITO
    testImplementation TestDependencies.ANDROID_ARC_TEST
    testImplementation TestDependencies.COROUTINES_TEST
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'

    androidTestImplementation TestDependencies.ESPRESSO_CONT
    androidTestImplementation TestDependencies.ESPRESSO
    androidTestImplementation TestDependencies.TEST_CORE
    androidTestImplementation TestDependencies.ANDROID_JUNIT_KTX
    debugImplementation(TestDependencies.TEST_FRAGMENT) {
        exclude group: 'androidx.test', module: 'monitor'
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Music-Search'
        property "sonar.projectDescription", "Music app with instant search"
        property "sonar.projectKey", PACKAGE_ID
        property 'sonar.projectVersion', VERSION_CODE
        property 'sonar.sources', 'src/main/java'
        property 'sonar.language', 'kotlin'
        property 'sonar.core.codeCoveragePlugin', 'jacoco'
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/coverage/debug/report.xml"

    }
}